#!/usr/bin/perl -w

use strict;
use Getopt::Long;


my $sort = 1;

GetOptions(
  'no-sort' => sub { $sort = 0; },
  'help'    => sub { usage() },
);

my %list;

while (<>) {
  $list{$_}++;
}

if ($sort) {
  my @keys = sort { $list{$b} <=> $list{$a} } keys %list;
  for my $k (@keys) {
    print "$list{$k}\t$k";
  }
} else {
  while ((my $k, my $v) = each %list) {
    print "$v\t$k";
  }
}

sub usage {
  print <<'USAGE';
Usage: count [--no-sort] [FILE]

Description:
Count the number of times that each line appears repeated in the input.
By default the output is sorted showing the most common values first.

-no-sort
  If speed is a concern, this option will not sort the output. As a hash is
  used to hold the values of the lines, the output will appear on a seemingly
  random order.
USAGE

  exit 1;
}
