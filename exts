#!/usr/bin/perl

use strict;
use warnings;
use YAML;
use Getopt::Long;

my $path = undef;
my $hidden = 0;

GetOptions(
  'path=s' => \$path,
  'ls'     => sub { $path = '.' },
  'hidden' => \$hidden,
  'h|help' => sub { usage() },
);

my $files = { };
if (defined $path) {
  opendir $files->{handle}, $path or die "Cannot open directory: $path\n$!\n";  
  $files->{fetch} = sub { return scalar readdir $files->{handle} };
  $files->{close} = sub { closedir $files->{handle} };
} else {
  $files->{fetch} = sub { return scalar <> };
  $files->{close} = sub { };
}

my %ext;
while (defined (my $file = $files->{fetch}->())) {
  chomp $file;
  next unless $file =~ m/\./;
  next unless $file =~ m/[^.]/;
  next if $file =~ m/^\./ && !$hidden;
  $file =~ s/^.+?\././;
  $ext{$file} = 1;
}
$files->{close}->();

print "$_\n" for sort keys %ext;

sub usage {
  print <<'USAGE';
Usage: exts [OPTIONS]

Description:
Given a list of files as input, output a list of all their file extensions.
The extension of a file is considered to be anything from the first dot to the
end of the file name. The list of files can be read from standard input (the
default) or from a directory in the system.

The following options are available:
-ls
  Instead of reading from standard input, get the list of files from the
  current directory.
  
-path DIR
  Instead of reading from standard input, get the list of files from the
  specified directory.

-hidden
  Show hidden files, the extension is considered to be the whole file name.
USAGE

  exit 1;
}
